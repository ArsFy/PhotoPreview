apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
group = 'com.github.wanggaowan'

// 打包源码jar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                // 同时发布源码
                artifact sourcesJar

                groupId = 'com.github.wanggaowan'
                artifactId = 'PhotoPreview'
                version = "2.4.5"
            }
        }

        repositories {
            maven {
                url = 'https://jitpack.io/'
            }
        }
    }
}

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode 27
        versionName "2.4.3"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // 设置JDK1.8
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation deps.appcompat
    implementation deps.recyclerview
    implementation deps.transition
    implementation deps.cardview
    // 为了兼容大部分用户仅依赖低版本，本库仅使用GifDrawable类型判断
    implementation 'com.github.bumptech.glide:glide:3.3.0'
}
